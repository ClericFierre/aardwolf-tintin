#CLASS hunt_trick KILL
#CLASS hunt_trick OPEN


# this alias is for turning off all the actions when we aren't hunting
#alias hqof
{
    #UNACTION {^%* is here!$};
    #UNACTION {You seem unable to hunt that target for some reason.};
    #UNACTION {^No one in this area by that name.};
    #UNACTION {^You are confident that %* passed through here, heading %*.};
    #UNACTION {^You are certain that %* is %* from here.};
    #UNACTION {^You have no idea what you're doing, but maybe %* {is|left} %*?};
    #UNACTION {^You couldn't find a path to %* from here.$};
}


# This includes support for casting 'huntmaster' when on a Global Quest but
# you will have to establish a way to set the 'gquest' variable to 1 on your
# own.
#
# Note: You need to set or modify the 'lower_targ' variable.

#alias hqq 
{
	#NOP Initialize the variable(s) and add the actions;
	#VAR hunt_trick_targ 1;
	#VAR hunt_trick_mob {$lower_targ};
    #if {"$GMCP[CHAR][BASE][subclass]" == "Hunter" && $gquest == 1} {cast 'huntmaster'};
	#ACTION {^%* is here!$}
	{
		#SHOW <069>[Hunt-Trick]<129>Mob in room!<099> - $hunt_trick_targ\.$hunt_trick_mob;
        where $hunt_trick_targ\.$hunt_trick_mob;
        hqof;
	};
	#ACTION {You seem unable to hunt that target for some reason.} 
	{
		#SHOW <069>[Hunt-Trick]<129>Right Mob!<099> - $hunt_trick_targ\.$hunt_trick_mob;
		where $hunt_trick_targ\.$hunt_trick_mob;
        hqof;
	};
	#ACTION {^No one in this area by that name.}
	{
		#SHOW <069>[Hunt-Trick]<099>Wrong area, wrong mob, or no_hunt.<099>;
		hqof;
	};
	#ACTION {^You are confident that %* passed through here, heading %*.}
	{
		#SHOW <069>[Hunt-Trick]<119>Wrong Mob.<099> - $hunt_trick_targ\.$hunt_trick_mob;
		#MATH {hunt_trick_targ} {$hunt_trick_targ+1};
		hunt $hunt_trick_targ\.$hunt_trick_mob
	};
	#ACTION {^You are certain that %* is %* from here.} 
	{
		#SHOW <069>[Hunt-Trick]<119>Wrong Mob.<099> - $hunt_trick_targ\.$hunt_trick_mob;
		#MATH {hunt_trick_targ} {$hunt_trick_targ+1};
		hunt $hunt_trick_targ\.$hunt_trick_mob
	};
	#ACTION {^You have no idea what you're doing, but maybe %* {is|left} %*?}
	{
		#SHOW <069>[Hunt-Trick]<119>Wrong Mob.<099> - $hunt_trick_targ\.$hunt_trick_mob;
        #MATH {hunt_trick_targ} {$hunt_trick_targ+1};
        hunt $hunt_trick_targ\.$hunt_trick_mob
	};
    #ACTION {^You couldn't find a path to %* from here.$}
	{
		#SHOW <069>[Hunt-Trick]<119>Wrong Mob.<099> - $hunt_trick_targ\.$hunt_trick_mob;
        #MATH {hunt_trick_targ} {$hunt_trick_targ+1};
        hunt $hunt_trick_targ\.$hunt_trick_mob
	};
	#NOP Ok, time to do something;
	#IF {"%1" == ""}
	{
		#var hunt_trick_mob {$lower_targ};
		hunt $hunt_trick_targ\.$hunt_trick_mob
	};
	#ELSE
	{
		#var hunt_trick_mob {%0};
		hunt $hunt_trick_targ\.$hunt_trick_mob
	}
}

#CLASS hunt_trick CLOSE
